import static org.apache.tools.ant.taskdefs.condition.Os.*

plugins {
    id 'com.google.protobuf'
}

description = 'RSocket RPC Library'

dependencies {
    compile project(':rsocket-ipc-core')
    implementation 'com.google.protobuf:protobuf-java:3.18.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'

    api 'io.opentracing:opentracing-api:0.33.0'
    api 'javax.inject:javax.inject:1'
    api 'javax.annotation:javax.annotation-api:1.3.2'
    api 'io.rsocket:rsocket-core:1.1.1'
    api 'io.micrometer:micrometer-core:1.7.4'

    protobuf project(':rsocket-rpc-metrics-idl')

    testImplementation 'io.opentracing.brave:brave-opentracing:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.0'

    testImplementation 'javax.inject:javax.inject:1'
    testImplementation 'io.projectreactor:reactor-test:3.4.10'
    testImplementation 'com.google.protobuf:protobuf-java:3.18.0'
    testImplementation 'org.histogram:HdrHistogram'
    testImplementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    testImplementation 'io.socket:rsocket-transport-netty'
    testImplementation 'io.socket:rsocket-transport-local'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.16.3'
}

def protocPluginBaseName = "rsocket-rpc-protobuf-${osdetector.os}-${osdetector.arch}"
def javaPluginPath = "$rootDir/rsocket-rpc-protobuf/build/exe/java_plugin/$protocPluginBaseName"

if(isFamily(FAMILY_WINDOWS)){
    javaPluginPath = javaPluginPath + ".exe"
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc"
    }
    plugins {
        rsocketRpc {
            path = javaPluginPath
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.dependsOn ':rsocket-rpc-protobuf:java_pluginExecutable'
            // Recompile protos when the codegen has been changed
            task.inputs.file javaPluginPath
            // Recompile protos when build.gradle has been changed, because
            // it's possible the version of protoc has been changed.
            task.inputs.file "${rootProject.projectDir}/build.gradle"
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}
